plugins {
    id 'java'
    id 'application'
    id 'io.freefair.lombok' version '8.3'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    compileOnly 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'
    implementation 'com.lmax:disruptor:3.4.2' // To fix async logging error, adjust the version number if needed
    implementation group: 'com.zaxxer', name: 'HikariCP-java7', version: '2.4.13'
    implementation group: 'org.apache.mina', name: 'mina-core', version: '2.0.21'
    implementation group: 'mysql', name: 'mysql-connector-java', version: '8.0.33'
    implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: '2.15.2'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.20.0' // Use the latest version
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.20.0' // Use the same version as log4j-api
    implementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.20.0' // Use the same version as log4j-api
}

sourceSets {
    main {
        java {
            srcDirs 'src'
        }
        resources {
            srcDirs 'configuration'
        }
    }
}

mainClassName = "net.server.Server"

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes(
                'Main-Class': mainClassName
        )
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    from 'configuration' // Include the log4j2.xml file
}

run {
    // Set the maximum heap size (Xmx)
    jvmArgs = ['-Xmx4g'] // Adjust the size as needed, e.g., '-Xmx1g' for 1 GB
    systemProperty 'java.util.concurrent.ForkJoinPool.common.parallelism', '16'
    systemProperty 'log4j2.contextSelector', 'org.apache.logging.log4j.core.async.AsyncLoggerContextSelector'
}

tasks.named('test', Test) {
    useJUnitPlatform()

    maxHeapSize = '1G'

    testLogging {
        events "passed"
    }
}
